import unqualified ValveDriver
import unqualified ValveHAL

port Armour {
  function setup(settings : ValveParameters) : Bool
  function isOpen() : Bool
  function isClosed() : Bool
  function open() : Bool
  function close() : Bool

  outgoing signal moveEnded(result : ValveDriver.Status)

  machine {
    var position : ValveDriver.Status = .Unknown
    isOpen() = position == .Opened
    isClosed() = position == .Closed

    open() = false
    close() = false
    setup(_) = false

    state Initial {
      setup(_) = nondet {
        InvalidParameters:
        {
          return false;
        },
        ValidParameters:
        {
          setNextState(Ready);
          return true;
        },
      }
    }

    state Ready {
      open() = {
        nondet {
          {
            position = .Opened;
            moveEnded(position);
          },
          {
            setNextState(Moving(.Opened))
          },
        }
        return true;
      }
      close() = {
        nondet {
          {
            position = .Closed;
            moveEnded(position);
          },
          {
            setNextState(Moving(.Closed))
          },
        }
        return true;
      }
    }

    state Moving(target : ValveDriver.Status) {
      entry() = {
        position = .Unknown;
      }

      spontaneous = {
        position = nondet {
          target,
          .Unknown,
          .Error,
        };
        setNextState(Ready);
      }

      exit() = {
        moveEnded(position);
      }
    }
  }
}
