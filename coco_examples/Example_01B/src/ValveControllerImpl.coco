import unqualified ValveController
import unqualified ValveHAL

@runtime(.MultiThreaded)
component ValveControllerImpl {
  val client : Provided<ValveController>
  val hal : Required<ValveHAL>

  machine {
    state Initial {
      client.setup() = setNextState(Unknown)
    }

    // Valve position unknown until the first move request is received
    state Unknown {
      client.open() = {
        hal.move(.Open);
        setNextState(Opened);
      }
      client.close() = {
        hal.move(.Close);
        setNextState(Closed);
      }
    }

    state Opened {
      client.open() = {}
      client.close() = {
        hal.move(.Close);
        setNextState(Closed);
      }
    }

    state Closed {
      client.open() = {
        hal.move(.Open);
        setNextState(Opened);
      }
      client.close() = {}
    }
  }
}
