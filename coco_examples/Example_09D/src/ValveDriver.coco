import unqualified ValveHAL

port ValveDriver {
  enum Status {
    case Unknown
    case Opened
    case Closed
    case Moving
    case Error
  }

  function setup(settings : ValveParameters) : Bool
  function open() : Nil
  function close() : Nil

  outgoing signal endOfMovement(result : Status)

  machine {
    // Initial position unknown
    var status : Status = .Unknown

    state Initial {
      setup(_) = nondet {
        ValidParameters:
        {
          setNextState(Ready);
          true
        },
        InvalidParameters: false,
      }
    }

    state Ready {
      open() = nondet {
        OpenMoveStarted: setNextState(Moving(.Opened)),
        OpenMoveFailedToStart: endOfMovement(.Error),
      }
      close() = nondet {
        CloseMoveStarted: setNextState(Moving(.Closed)),
        CloseMoveFailedToStart: endOfMovement(.Error),
      }
    }

    state Moving(target : Status) {
      spontaneous = {
        status = nondet {
          MoveSucceeded: target,
          MoveFailed: .Error,
        };
        endOfMovement(status);
        setNextState(Ready);
      }
    }
  }
}
